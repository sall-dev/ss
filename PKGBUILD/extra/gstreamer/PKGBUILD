# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich
#  - remove valgrind makedepend
#  - move gtk3 from checkdepend to makedepend

pkgname=gstreamer
pkgver=1.16.2
pkgrel=1
pkgdesc="GStreamer open-source multimedia framework core library"
url="https://gstreamer.freedesktop.org/"
arch=(x86_64)
license=(LGPL)
depends=(libxml2 glib2 libunwind libcap libelf)
makedepends=(gtk-doc gobject-introspection git bash-completion gtk3 meson)
checkdepends=(gmp gsl)
_commit=129493687793cbc109d6211bb0e465218e383e9d  # tags/1.16.2^0
install=gstreamer.install
source=("git+https://gitlab.freedesktop.org/gstreamer/gstreamer.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D ptp-helper-permissions=capabilities \
    -D dbghelp=disabled \
    -D gobject-cast-checks=disabled \
    -D glib-asserts=disabled \
    -D glib-checks=disabled \
    -D package-name="GStreamer (Arch Linux)" \
    -D package-origin="https://www.archlinux.org/"
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
