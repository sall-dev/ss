# Maintainer: Bruno Pagani <archange@archlinux.org> 

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix 32-bit builds

pkgname=nsjail
pkgver=2.9
pkgrel=4
pkgdesc="Light-weight process isolation tool"
arch=(x86_64)
url="https://nsjail.com"
license=(Apache)
depends=(protobuf libnl)
makedepends=(git)
source=("https://github.com/google/nsjail/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'git+https://github.com/google/kafel.git#commit=722b93a630e53ca472a4c69f61cb3dfc44ea5041'
        '0001-32-bit-fix.patch')
sha256sums=('f8578a48330d3d089c9234a2b562be08043f8fe77453c322724acdf403d0afc3'
            'SKIP'
            '3187c9980a1c0a91ac3e0302bf8fdbf87126c6e29679aa4c36322790efed4db2')

prepare() {
    mv kafel ${pkgname}-${pkgver}
    sed -e 's|-Werror||' -i $pkgname-$pkgver/Makefile
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../0001-32-bit-fix.patch
}

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}
    install -Dm755 nsjail -t "${pkgdir}"/usr/bin/ 
}
