# Maintainer: Antonio Rojas <arojas@archlinux,org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on vc

pkgname=krita
pkgver=4.2.8.2
pkgrel=2
pkgdesc="Edit and paint images"
arch=(x86_64)
url="https://krita.org"
license=(GPL3)
depends=(kitemviews kitemmodels ki18n kcompletion karchive kguiaddons kcrash qt5-svg qt5-multimedia quazip
         gsl libraw exiv2 openexr fftw curl boost-libs giflib hicolor-icon-theme desktop-file-utils)
makedepends=(extra-cmake-modules kdoctools boost eigen poppler-qt5 opencolorio python-pyqt5 libheif
             qt5-tools sip python-sip)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations' 'opencolorio: for the LUT docker'
            "krita-plugin-gmic: G'MIC plugin" 'python-pyqt5: for the Python plugins' 'libheif: HEIF filter')
conflicts=(calligra-krita krita-l10n)
replaces=(calligra-krita krita-l10n)
source=("https://download.kde.org/stable/krita/${pkgver%.*}/$pkgname-$pkgver.tar.gz"{,.sig}
         kdebug-410867.patch::"https://invent.kde.org/kde/krita/commit/ca07d42c.patch"
         kdebug-414672.patch::"https://invent.kde.org/kde/krita/commit/2dc2ed5f.patch")
sha256sums=('80f741fa11245ff20a99f529f5f62def17791f3ad2c9aec5c5cf5440ae1fcba7'
            'SKIP'
            'cfc9c7b2accbb730be3d9e2c92e5d501d2e3465841c97efae84602987e8cae3b'
            '181802a9273133a654f6aac2b1760b91beffa8e631b2aa162b5b10d342d5f712')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD') # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-410867.patch # Fix crash on close
  patch -p1 -i ../kdebug-414672.patch # Fix crash on transform tool
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
