# Maintainer: Christian Hesse <mail@eworm.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add -fsigned-char to CXXFLAGS

pkgname=gimp-nufraw
pkgver=0.42
pkgrel=6
pkgdesc='Converter for raw files; utility and GIMP plugin'
url='https://sourceforge.net/projects/nufraw/'
arch=('x86_64')
license=('GPL')
depends=('cfitsio' 'exiv2' 'gtkimageview' 'lcms2' 'lensfun' 'libjpeg-turbo')
makedepends=('gimp')
conflicts=('gimp-ufraw')
replaces=('gimp-ufraw')
optdepends=('gimp: to use the gimp import plugin for raw images')
source=("https://downloads.sourceforge.net/nufraw/nufraw-$pkgver.tar.gz"
        '0001-nufraw-iostream.patch'
        '0002-nufraw-exiv2-0.27.patch'
        '0003-omp_definitions.patch')
sha256sums=('ddfb592a183618c802e990211f6efce75f4801a7708ecc364666f53af25c9f90'
            'fd0120af2cfa8783edd3d91a39b638fbb30ced8c692815be230a302bc55a71ad'
            'ec7caabbdaa8fb67097bfb72c26a1140754e184e756ec2013863af37eed67220'
            '8f15799e1e682330072f04af4607380b91a55edc9c9f638bdeae1ee3d2318ccf')

prepare() {
  cd "nufraw-$pkgver"

  # Add missing include
  patch -p1 -i ../0001-nufraw-iostream.patch
  
  # Fix build with exiv2 0.27 (Gentoo)
  patch -p1 -i ../0002-nufraw-exiv2-0.27.patch

  # fix '... not specified in enclosing ‘parallel’'
  patch -p1 -i ../0003-omp_definitions.patch

  autoreconf -fi

  mv nufraw.desktop{.desktop,}
  chmod +x generate_schemas.sh
}

build() {
  cd "nufraw-$pkgver"

  CXXFLAGS+=" -fsigned-char"

  ./configure \
    --prefix=/usr \
    --enable-extras \
    --enable-mime \
    --enable-openmp \
    --with-gimp \
    --with-cinepaint \
    --enable-contrast
  make
}

package() {
  make -C "nufraw-$pkgver" DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/bin/dcraw"
}

