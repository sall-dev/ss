# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - ARM patch, use NO_CRASH_HANDLER=1 for AArch64

pkgname=texstudio
pkgver=2.12.22
pkgrel=1
pkgdesc="Integrated writing environment for creating LaTeX documents"
arch=('x86_64')
url="http://texstudio.sourceforge.net/"
license=('GPL')
depends=('poppler-qt5' 'qt5-svg' 'qt5-script' 'libxkbcommon-x11' 'hicolor-icon-theme' 'hunspell' 'quazip' 'desktop-file-utils')
makedepends=('qt5-tools' 'mercurial' 'imagemagick' 'librsvg')
optdepends=('evince: pdf reader'
            'okular: alternate pdf reader')
replaces=('texmakerx')
source=("https://github.com/texstudio-org/texstudio/archive/${pkgver}.tar.gz"
        fs54269.patch
        fs56433.patch
        texstudio-debughelper-arm-fix.patch)
sha512sums=('c589285d1fba00812583a3d58602a99e044eb4bcf6a84b939d00ad7fe4ba8f15788031361fc9b4cb8df19f8098911c437d1de5459c1736fa40888b88d13b10d1'
            '247e85f668b06c8a2def4e7456f0ddc8d35c7990484c1c037f7cf43fdee91419b5df66e968ef1e0c637fe685722bbad9171bdd79fad7a10912ae329aff088b68'
            'a098c45440077db6c137bbeeb76d266242b80d51c3721aed30987d22b3e381b2a490b685aff4897d203a17256c64a9254360e3ac6f962675ea4b9a575a29a808'
            'c8a915846d355593c2ebd0c9ebd5dfa908c456a4a51c4d63f1f685efdf2ff763e68fdaa34ddfe602890f363486ed0b9be55e09112904f60fbd0de40a418947d3')

prepare() {
    cd texstudio-${pkgver}/src
    patch -Np1 < "${srcdir}"/fs54269.patch
    patch -Np1 < "${srcdir}"/fs56433.patch
    patch -Np2 < "${srcdir}"/texstudio-debughelper-arm-fix.patch
}

build() {
    cd texstudio-${pkgver}
    [[ $CARCH == "aarch64" ]] && EXTRA="NO_CRASH_HANDLER=1"
    qmake-qt5 CONFIG-="debug" $EXTRA USE_SYSTEM_QUAZIP=1 USE_SYSTEM_HUNSPELL=1 texstudio.pro
    make
}

package() {
    cd texstudio-${pkgver}
    make INSTALL_ROOT="${pkgdir}" install

    for res in $(/bin/ls /usr/share/icons/hicolor | grep '[0-9]\+x[0-9]\+'); do
        mkdir -p "${pkgdir}"/usr/share/icons/hicolor/${res}/apps
        convert +set date:create +set date:modify -background none -density 1200 -resize ${res} \
            "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/texstudio.svg \
            "${pkgdir}"/usr/share/icons/hicolor/${res}/apps/texstudio.png
    done
}
