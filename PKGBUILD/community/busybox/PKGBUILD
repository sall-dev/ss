# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix 32-bit FTBFS with new musl

pkgname=busybox
pkgver=1.31.1
pkgrel=2
pkgdesc="Utilities for rescue and embedded systems"
arch=("x86_64")
url="https://www.busybox.net"
license=('GPL')
makedepends=("ncurses" "musl" "kernel-headers-musl")
validpgpkeys=('C9E9416F76E610DBD09D040F47B70C55ACC9965B')
source=("$url/downloads/$pkgname-$pkgver.tar.bz2"{,.sig}
        "config"
        "0001-comment-time-size-check.patch")
sha256sums=('d0f940a72f648943c1f2211e0e3117387c31d765137d92bd8284a3fb9752a998'
            'SKIP'
            '3fc329e671bcff93b976ed9f6ca47eaa53468811eccae2ad50ca50611d37004e'
            'd390c798540bf4369da42a575369e737e3e270c7d3c6c2bd141d1da939f4d219')
b2sums=('95c2345bc715eb7a1f08816a40add9b7a6176ef509a84069343ec2caddec58d8d57a07676ca2b03c209734169321433e27ba963b4c8b98dbfda4e409dcc347b4'
        'SKIP'
        'de6cfe15b2c1c18e049f64f4ad67447f5db80076eb7fed83b09b7014dbd1ce55f0b382bedcfaecbcd205c3366d045cde1db7e2d7046715446f76466fd9ea6b7b'
        '2a227153f936850778a772794c4eaa3b72157b76d07ecd9b491f19c690dff258e2c6a755ef2372136235a5220688847e9357812e95d8bdc5550593831b2d592d')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i ../0001-comment-time-size-check.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cp "$srcdir"/config .config
  # reproducible build
  export KCONFIG_NOTIMESTAMP=1
  make CC=musl-gcc
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm0755 busybox "$pkgdir"/usr/bin/busybox

  # docs
  install -Dm644 docs/busybox.1 "$pkgdir"/usr/share/man/man1/busybox.1
  for doc in BusyBox.html BusyBox.txt; do
    install -Dm644 docs/$doc "$pkgdir"/usr/share/doc/$pkgname/$doc
  done
}
