# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Grey Christoforo <first name [at] last name [dot] net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - -DBUILD_TESTS=OFF

pkgname=curaengine
pkgver=4.4.0
pkgrel=1
pkgdesc="Engine for processing 3D models into 3D printing instruction for Ultimaker and other GCode based 3D printers."
url="https://github.com/Ultimaker/CuraEngine"
arch=('x86_64')
license=('AGPL')
depends=('arcus' 'rapidjson' 'polyclipping')
# TODO: https://github.com/Ultimaker/CuraEngine/blob/master/CMakeLists.txt#L26
# Work with system openmp
# Package polyclipping in the repos
checkdepends=('cppunit')
makedepends=('cmake' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Ultimaker/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('aa2396a6bd2c32ae2ceaad32a9cc986cf2301b188543c333a848df0434d31b5ce22a923e2cd60bc61c313fee8f2c4a4fc30b083ec1f99e2cb6df5c92c6f37451')


build() {
  cd CuraEngine-${pkgver}
  mkdir -p build
  cd build
    
   cmake .. \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DBUILD_TESTS=OFF \
     -DCMAKE_BUILD_TYPE=Release

  make
}

check() {
  cd CuraEngine-${pkgver}/build
  make test
}

package() {
  cd CuraEngine-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
