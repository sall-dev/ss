# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - define aarch64

buildarch=8

[[ "${CARCH}" == "x86_64" ]] && _EFI_ARCH="x86_64"
[[ "${CARCH}" == "i686" ]] && _EFI_ARCH="ia32"
[[ "${CARCH}" == "aarch64" ]] && _EFI_ARCH="aarch64"

_pkgver=3.0.11
pkgname="gnu-efi-libs"
pkgver="${_pkgver}"
pkgrel=1
pkgdesc="Library for building UEFI Applications using GNU toolchain"
url="https://sourceforge.net/projects/gnu-efi/"
license=('GPL')
arch=('x86_64')
options=('!strip' '!makeflags' '!buildflags')
makedepends=('pciutils')
source=("https://download.sourceforge.net/gnu-efi/gnu-efi-${pkgver}.tar.bz2")
md5sums=('21148bbcccec385a9bfdf5f678959577')

build() {
	cd "${srcdir}/gnu-efi-${_pkgver}/"

	make lib gnuefi inc apps
}

package() {
	cd "${srcdir}/gnu-efi-${_pkgver}/"

	make INSTALLROOT="${pkgdir}" PREFIX="/usr" LIBDIR="/usr/lib" install

	install -d "${pkgdir}/usr/share/gnu-efi/apps/${_EFI_ARCH}/"
	install -D -m0644 "${srcdir}/gnu-efi-${_pkgver}/${_EFI_ARCH}/apps"/*.efi "${pkgdir}/usr/share/gnu-efi/apps/${_EFI_ARCH}/"
}
