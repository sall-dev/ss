# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Stijn Segers <francesco dot borromini at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - define WITH_NEON and ARM_FP_ABI

_name=FreeRDP
pkgname=freerdp
epoch=1
pkgver=2.0.0_rc4
pkgrel=8
pkgdesc="Free implementation of the Remote Desktop Protocol (RDP)"
arch=('x86_64')
url="http://www.freerdp.com/"
license=('Apache')
depends=('dbus-glib' 'glib2' 'glibc' 'gsm' 'gstreamer' 'gst-plugins-base-libs'
'lame' 'libcups' 'libjpeg-turbo' 'libgssglue' 'libsoxr' 'libusb' 'libxkbcommon'
'libxinerama' 'libxkbfile' 'libxrandr' 'mbedtls' 'openssl' 'pam' 'pcsclite'
'wayland')
makedepends=('alsa-lib' 'cmake' 'docbook-xsl' 'faac' 'faad2' 'ffmpeg' 'krb5'
'libpulse' 'systemd-libs' 'xmlto' 'xorgproto')
provides=('libwinpr-tools2.so' 'libfreerdp-client2.so' 'libfreerdp2.so'
'libwinpr2.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver/_/-}.tar.gz"
        "$pkgname-2.0.0-manpage_formatting.patch")
sha512sums=('b4a4d4a58d09010bc45fb90cca148dc4421a4cf0cd5caf288aa702212ef081f14fc418b91f1b79ec8631f582c9ebcdd3031d3333b6a892adb29c402492abb649'
            'd960e042d1527b5d5721136b6b20fc36f65beafd010581ea5b908668537fe9fe622de6689a29c0274b0d6f1e513615f0d02e56c1d1d1e613d093e145d39af8d7')

prepare() {
  mv -v "${_name}-${pkgver/_/-}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fix man page formatting:
  # https://bugs.archlinux.org/task/64814
  patch -Np1 -i "../${pkgname}-2.0.0-manpage_formatting.patch"
}

build() {
  cd "${pkgname}-${pkgver}"

  [[ $CARCH == "arm" ]] && ABI="soft"
  [[ $CARCH == "armv6h" || $CARCH == "armv7h" ]] && ABI="hard"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_NEON=OFF \
	-DARM_FP_ABI=${ABI} \
        -DWITH_MBEDTLS=ON \
        -DWITH_PULSE=ON \
        -DWITH_CUPS=ON \
        -DWITH_PCSC=ON \
        -DWITH_JPEG=ON \
        -DWITH_GSM=ON \
        -DWITH_LAME=ON \
        -DWITH_FAAD2=ON \
        -DWITH_FAAC=ON \
        -DWITH_SOXR=ON \
        -DWITH_SERVER=ON \
        -DWITH_CHANNELS=ON \
        -DWITH_CLIENT_CHANNELS=ON \
        -DWITH_SERVER_CHANNELS=ON \
        -DCHANNEL_URBDRC_CLIENT=ON \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so' 'libavcodec.so' 'libavutil.so' 'libfaac.so'
  'libfaad.so' 'libpulse.so' 'libsystemd.so' 'libudev.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 {ChangeLog,README} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
