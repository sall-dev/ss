# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use PIC with AArch64

pkgname=gpac
pkgver=0.8.0
pkgrel=1
epoch=1
pkgdesc='A multimedia framework based on the MPEG-4 Systems standard'
arch=(x86_64)
url=https://gpac.wp.imt.fr/
license=(LGPL)
depends=(
  ffmpeg
  glu
  libxv
)
makedepends=(
  git
  jack
  a52dec
  freetype2
  faad2
  libmad
  mesa
  sdl2
)
optdepends=(
  'jack: for jack support'
  'a52dec: for A52 support'
  'faad2: for AAC support'
  'libmad: for mp3 support'
  'sdl2: for sdl support'
)
provides=(libgpac.so)
options=(staticlibs)
source=(git+https://github.com/gpac/gpac.git#tag=v${pkgver})
sha256sums=('SKIP')

build() {
  cd gpac

  [[ $CARCH == 'aarch64' ]] && CONFIG='--enable-pic'

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --X11-path=/usr \
    --use-js=no $CONFIG
  make
}

package() {
  cd gpac

  make DESTDIR="${pkgdir}" STRIP=true install install-lib
}

# vim: ts=2 sw=2 et:
