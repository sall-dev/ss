# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - link against libatomic for v5/v6

pkgname=openimageio
pkgver=2.1.11.2
pkgrel=1
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=(x86_64)
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew' 'libtiff' 'opencolorio' 'intel-tbb' 'openssl' 'libpng' 'libraw' 'libwebp')
makedepends=('cmake' 'qt5-base' 'python' 'boost' 'mesa' 'git' 'freetype2' 'fontconfig' 'libxrender')
optdepends=('qt5-base: iv image viewer'
            'python: bindings support')
source=(git+git://github.com/OpenImageIO/oiio.git#tag=Release-${pkgver})
md5sums=('SKIP')

prepare() {
  mkdir oiio/build
}

build() {
  cd oiio/build

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && LDFLAGS="-latomic $LDFLAGS"

  cmake .. \
      -DUSE_PYTHON=ON \
      -DPYTHON_VERSION=3.8 \
      -DUSE_OPENSSL=ON \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DOIIO_BUILD_TESTS=ON \
      -DOIIO_BUILD_TOOLS=ON \
      -DSTOP_ON_WARNING=OFF
  make
}

package() {
  cd oiio/build

  make DESTDIR="$pkgdir" install

  install -Dm644 ../LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE".md
}
