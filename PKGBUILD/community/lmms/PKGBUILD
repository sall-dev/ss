# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Storyteller <spiralsorrow@hotmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove wine makedepend and optdepend
#  - remove carla makedepend
#  - remove -DWINE_INCLUDE_DIR and -DWINE_LIBRARY from cmake command
#  - upstream rpmalloc fix for ARM

pkgname=lmms
pkgver=1.2.1
pkgrel=3
pkgdesc='The Linux MultiMedia Studio'
arch=('x86_64')
url="https://lmms.io"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'fftw' 'fltk' 'fluidsynth' 'gcc-libs' 'glibc' 'lame'
'libgig.so' 'libogg' 'libportaudio.so' 'libpulse' 'libsamplerate' 'libsndfile'
'libvorbis.so' 'libvorbisenc.so' 'libvorbisfile.so' 'libx11' 'qt5-base'
'qt5-x11extras' 'sdl' 'stk')
makedepends=('cmake' 'doxygen' 'extra-cmake-modules' 'freetype2'
'ladspa' 'qt5-tools')
optdepends=('pulseaudio: pulseaudio support')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/v${pkgver}/${pkgname}_${pkgver}.tar.xz"
        'https://github.com/rampantpixels/rpmalloc/commit/1c580b394e1a20e63d63c72cad421f7be4422089.patch')
sha512sums=('7054d1592fdea60d76423a0260e941c217f50d2db9c4fcc35df20c8b58067c431ed9b264ef05e9f3f2cba84ca66870d1bf23da92b294a00ac6d567e3d3e771d6'
            '95b8df46fbb1194d30ba9dd34e7b0325590c63a1a2f34730fa62c00fea1a75e728733ee3c287ed2b31150e66aa18e170fe4725a0e7cad2518039a19b12dc389c')

prepare() {
  cd "${pkgname}-${pkgver}"
  mkdir -vp build
  # setting lib dir
  sed -e 's|lib64|lib|g' -i cmake/modules/DetectMachine.cmake
  # upstream rpmalloc fix for ARM
  patch -p1 -d src/3rdparty/rpmalloc/rpmalloc -i "${srcdir}/1c580b394e1a20e63d63c72cad421f7be4422089.patch"
}

build() {
  cd "${pkgname}-${pkgver}/build"
  export LDFLAGS="${LDFLAGS}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DWANT_QT5=ON \
        -DWANT_SOUNDIO=OFF \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_MODULE_PATH=/usr/share/ECM/find-modules \
        -DCMAKE_PREFIX_PATH=/usr/include/wine/windows \
        -DOpenGL_GL_PREFERENCE=GLVND \
        ..
  make
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
}
