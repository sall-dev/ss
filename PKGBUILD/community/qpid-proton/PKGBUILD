# Maintainer: Nicola Squartini <tensor5@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable LTO

pkgname=(qpid-proton python2-qpid-proton)
pkgver=0.30.0
pkgrel=1
pkgdesc='High-performance, lightweight messaging library'
arch=('x86_64')
url='https://qpid.apache.org/proton'
license=('Apache')
makedepends=('cmake' 'doxygen' 'python-setuptools' 'python2-setuptools' 'swig' 'jsoncpp')
source=("https://www.apache.org/dist/qpid/proton/${pkgver}/qpid-proton-${pkgver}.tar.gz")
sha512sums=('0e55d6a2c9803ef72604c50c7b934379092680b5edd52e3641a851f50ba1136bfa83cd30328d2b3235152331109d3204aa32168aa626d6e8e34a590d82553cc8')

build() {
    cd ${pkgname}-${pkgver}

    mkdir build
    cd build
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_SUFFIX='' \
        -DSYSINSTALL_BINDINGS=ON \
        -DENABLE_LINKTIME_OPTIMIZATION=OFF \
        ..
    make

    cd python/dist
    python2 setup.py build
}

package_qpid-proton() {
    depends=('jsoncpp' 'libjsoncpp.so' 'python' 'ruby')

    cd ${pkgname}-${pkgver}/build
    make DESTDIR="${pkgdir}" install
}

package_python2-qpid-proton() {
    depends=('python2')

    cd qpid-proton-${pkgver}/build/python/dist
    python2 setup.py install --root="${pkgdir}" --skip-build --optimize=1
}
