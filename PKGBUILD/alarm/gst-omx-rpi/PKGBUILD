# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Kiril Zyapkov <kiril.zyapkov@gmail.com>
# Contributor: Cyril Lashkevich <notorca@gmail.com>

buildarch=20

pkgname=gst-omx-rpi
pkgver=1.16.0
pkgrel=1
pkgdesc="Gstreamer OpenMAX IL wrapper Plugin for Raspberry Pi"
arch=('armv6h' 'armv7h')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('gst-plugins-base-libs')
makedepends=('raspberrypi-firmware' 'python2')
options=(!libtool)
source=(http://gstreamer.freedesktop.org/src/gst-omx/gst-omx-$pkgver.tar.xz)
md5sums=('c6f8554513980682099a2a9832250b01')

build() {
  cd $srcdir/gst-omx-$pkgver

  export LDFLAGS+=' -L/opt/vc/lib'
  export CPPFLAGS+=' -I/opt/vc/include -I/opt/vc/include/IL -I/opt/vc/include/interface/vcos/pthreads -I/opt/vc/include/interface/vmcs_host/linux'
  export PKG_CONFIG_PATH="/opt/vc/lib/pkgconfig"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --with-package-name="GStreamer OpenMAX IL wrapper Plugin (ArchlinuxARM/RPi)" \
    --with-package-origin="http://www.archlinuxarm.org/" \
    --with-omx-target=rpi
  
  make
}

package() {
  cd gst-omx-$pkgver
  make DESTDIR="${pkgdir}" install
}
