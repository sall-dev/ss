# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Martin Striz <ms@poruba.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed dependency on numactl

pkgname=irqbalance
pkgver=1.6.0
pkgrel=1
pkgdesc="IRQ balancing daemon for SMP systems"
arch=('x86_64')
url="https://github.com/irqbalance/irqbalance"
license=('GPL2')
backup=('etc/irqbalance.env')
depends=('glib2' 'libcap-ng' 'systemd-libs')
makedepends=('systemd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v$pkgver.tar.gz")
sha512sums=('ce89dc41eb3aab5950a56b4b1c817aae5e87eaa84bc69bf88618caf53386513e488bb0b4724a600ae175c96be509367cfc418c7f85531007f80e02f7de549b16')

prepare() {
  cd "$pkgname-$pkgver"
  # fix location of configuration and binary in service
  sed -e 's|/path/to/|/etc/|g' \
      -e 's|/usr/sbin|/usr/bin|g' \
      -i "misc/${pkgname}.service"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sbindir=/usr/bin --with-systemd
  make
}

package() {
  cd "$pkgname-$pkgver"
  make install DESTDIR="$pkgdir"
  install -vDm 644 misc/irqbalance.service \
    -t "${pkgdir}/usr/lib/systemd/system/"
  install -vDm 644 misc/irqbalance.env -t "${pkgdir}/etc/"
  install -vDm 644 {AUTHORS,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
