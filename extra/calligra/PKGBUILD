# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on vc
#  - fedora patch to fix ARM build with GCC 6

pkgname=calligra
pkgdesc="A set of applications for productivity and creative usage"
pkgver=3.1.0
pkgrel=26
arch=(x86_64)
url='https://www.calligra-suite.org/'
license=(FDL1.2 GPL2 LGPL)
depends=(kcmutils knotifyconfig kross kactivities kdiagram libspnav kdelibs4support
         gsl openexr kcontacts qca-qt5 poppler-qt5 libodfgen qt5-webkit cauchy khtml)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin boost openjpeg akonadi-contacts
             libwpg okular eigen marble-common pstoedit libvisio libetonyek libwps libgit2 kinit)
optdepends=('libwpg: Corel WordPerfect Graphics image importer'
            'libwps: Microsoft Works file word processor format import'
            'libvisio: Microsoft Visio import filter'
            'libetonyek: Apple Keynote import filter'
            'pstoedit: EPS to SVG filter'
            'poppler: PDF to SVG filter'
            'openjpeg: PDF to SVG filer'
            'libgit2: Calligra Gemini git plugin'
            'kirigami2: for Calligra Gemini'
            'qt5-quickcontrols: for Calligra Gemini'
            'qt5-webengine: for Calligra Gemini')
conflicts=(calligra-devtools calligra-extras calligra-filters calligra-karbon karbon calligra-libs
           calligra-plugins calligra-sheets calligra-stage calligra-words calligra-l10n calligra-meta
           calligra-flow calligra-braindump calligra-gemini calligra-handbook)
replaces=(calligra-devtools calligra-extras calligra-filters calligra-karbon karbon calligra-libs
          calligra-plugins calligra-sheets calligra-stage calligra-words calligra-l10n calligra-meta)
for _lang in bs ca ca@valencia cs da de el en_gb es et fi fr gl hu it ja kk nb nl \
             pl pt pt_br ru sk sv uk zh_cn zh_tw; do
  conflicts+=(calligra-l10n-$_lang)
  replaces+=(calligra-l10n-$_lang)
done
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        calligra-2.9.11-gcc6_arm.patch
        calligra-qt5.11.patch
        calligra-qt5.13.patch
        calligra-poppler-0.69.patch::"https://cgit.kde.org/calligra.git/patch/?id=a6157182"
        calligra-poppler-0.71.patch
        calligra-poppler-0.72.patch
        calligra-poppler-0.73.patch
        calligra-poppler-0.82.patch
        calligra-poppler-0.83.patch)
sha256sums=('6818cd6e64136321be217eb57cc7d6ac7c7035191fdb1ee336ebe60bc114e870'
            'SKIP'
            '33d803c49abefc37f51f824543e889f9e369e86df9ce64d980e0a7dbca2ea27d'
            '40ac9fabed8e13c31f6f243bd7e9083c3ec322568991ba84987106b982ffc10a'
            'd32a9cda1343f224f5a0cfa2b8122c5571a0dbb09c9948414d6770d9c3814f4d'
            '127cc5e0946fbf741279306b4e9d592f5f901a7fab09039fcfc6c7e60193fa80'
            '052adc4ea812d7667812ff7867e703327dbe7c1ecad86094fd6772dc09782d87'
            '87a09710b9e7e825a46344ddec08eb98b3454d6d067b0177d284733b2342cece'
            'ef95f49f69c9f2a5b530e416003f2d6d106bb3c85e8458ae27494c64c8b474a8'
            'a2f02dccd8f7748b187dba805220ded95c41bbddc084091794691164184d1b82'
            '3877210cc58c11472909c62bdc1678a05deb30051f0180c47893e51bae9182d1')
validpgpkeys=(05D00A8B73A686789E0A156858B9596C722EA3BD  # Boudewijn Rempt <foundation@krita.org>
              42A60D06F449E895F40F09C01638B8A616108B8A) # Dag Andersen <danders@get2net.dk>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix build with Qt 5.11
  patch -p1 -i ../calligra-qt5.11.patch
# Fix build with Qt 5.13
  patch -p1 -i ../calligra-qt5.13.patch
# Fix build with poppler 0.69
  patch -p1 -i ../calligra-poppler-0.69.patch
# Fix build with poppler 0.71
  patch -p1 -i ../calligra-poppler-0.71.patch
# Fix build with poppler 0.72
  patch -p1 -i ../calligra-poppler-0.72.patch
# Fix build with poppler 0.73
  patch -p1 -i ../calligra-poppler-0.73.patch
# Fix build with poppler 0.82
  patch -p1 -i ../calligra-poppler-0.82.patch
# Fix build with poppler 0.83
  patch -p1 -i ../calligra-poppler-0.83.patch

  patch -p1 -i ../calligra-2.9.11-gcc6_arm.patch
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
