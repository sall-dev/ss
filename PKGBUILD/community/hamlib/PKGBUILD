# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - autoreconf
#  - --disable-uhd

pkgname=hamlib
pkgver=3.3
pkgrel=6
pkgdesc='Ham radio equipment control libraries'
arch=('x86_64')
url='https://hamlib.github.io'
license=('GPL' 'LGPL')
depends=('perl' 'python' 'lua' 'tcl' 'libxml2' 'libusb-compat' 'libtool' 'libuhd')
makedepends=('swig')
source=("https://github.com/Hamlib/Hamlib/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('4cf6c94d0238c8a13aed09413b3f4a027c8ded07f8840cdb2b9d38b39b6395a4a88a8105257015345f6de0658ab8c60292d11a9de3e16a493e153637af630a80'
            'SKIP')
validpgpkeys=(82D64F6B0E67CD41F689BBA6FB2C5130D55A8819)

prepare() {
  sed -i 's|usrp|uhd|g' $pkgname-$pkgver/configure.ac
}

build() {
  cd $pkgname-$pkgver

  autoreconf -fi
  ./configure \
  	--prefix=/usr \
  	--sbindir=/usr/bin \
  	--with-perl-binding \
  	--with-python-binding \
  	--with-tcl-binding \
  	--with-lua-binding \
  	--with-xml-support \
  	--disable-uhd

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
