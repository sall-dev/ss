# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix up makefile for 32-bit builds

pkgname=lrs
pkgver=070.a
_pkgver=${pkgver/./}
pkgrel=2
pkgdesc="C implementation of the reverse search algorithm for vertex enumeration/convex hull problems"
arch=(x86_64)
url="http://cgm.cs.mcgill.ca/~avis/C/lrs.html"
license=(GPL)
depends=(gmp)
source=("http://cgm.cs.mcgill.ca/~avis/C/lrslib/archive/lrslib-$_pkgver.tar.gz"
        lrs-system-cflags.patch
        0001-32-bit-changes.patch)
sha256sums=('d38cb8633e856398b461b35daa269d960c2c0e12b3df5725359dc3940b518e0c'
            '23814d543e458f43b9871f5de6112f3ca331d8ded3449c7e2efa681ca5a6606f'
            'af8dee8dffc5483a87f77401e5ac8c41d7506c9dfb202d07ad81dc0396846f4a')

prepare() {
  cd lrslib-${pkgver%.*}
  patch -p1 -i ../lrs-system-cflags.patch # Use system CFLAGS/LDFLAGS
  if [[ $CARCH != "aarch64" ]]; then
    patch -p1 -i ../0001-32-bit-changes.patch
  fi
}

build() {
  cd lrslib-${pkgver%.*}
  make -j1
}

package() {
  cd lrslib-${pkgver%.*}
  make DESTDIR="$pkgdir" prefix=/usr install
}
