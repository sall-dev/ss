# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add sse=0 to objcryst make line

pkgname=genxrdpattern
pkgver=1.1
pkgrel=1
pkgdesc="Uses ObjCryst++ to generate a theoretical XRD pattern"
arch=(x86_64)
url="https://github.com/psavery/genXrdPattern"
license=(GPL)
depends=()
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/psavery/genXrdPattern/archive/$pkgver-linux.tar.gz"
        https://github.com/psavery/genXrdPattern/releases/download/$pkgver-linux/objcryst.tgz)
sha256sums=('9bc01dbf637d6520d8e2f5743bb1b46f8319432af5ce2d2a484a36615c1586d3'
            '5523eae9937388260b3e491a1a11a443ab00a0961318ec92eee08f9366a0953f')

prepare() {
  mkdir -p build

  sed -e 's|/user/psavery/src/objcryst/Fox/src|.|g' -i objcryst/Fox/src/Fox.dep
}

build() {
  cd objcryst/Fox
  make Fox-nogui shared=0 sse=0 -j1

  cd "$srcdir"/build
  cmake ../genXrdPattern-$pkgver-linux \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DObjCryst_ROOT="$srcdir"/objcryst
  make
}

package() {
  cd build
  install -Dm755 genXrdPattern -t "$pkgdir"/usr/bin
}
